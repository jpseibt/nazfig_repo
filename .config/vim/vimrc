set nocompatible

call plug#begin()
Plug 'bluz71/vim-nightfly-colors', { 'as': 'nightfly' }
Plug 'gcmt/taboo.vim'
call plug#end()
colorscheme nightfly "let s:black = '#000d1a' on /vimfiles/plugged/nightfly/autoload/nightfly.vim
filetype plugin on

syntax enable
set number            " Show line numbers
set norelativenumber  " !Show relative line numbers
set scrolloff=0       " Keep no lines of context around the cursor
set backspace=indent,eol,start
set showcmd
set showcmdloc=last   " Show commands on the last line
set laststatus=2      " Always show statusline
set statusline=%F\ %m%r\ %=%l,%c\ %p%%\|%L
set formatoptions-=c fo-=r fo-=o fo-=t" Disable comment leader insertion and automatic line breaks


"==============================
" Compiling & Debugging
"==============================
let s:is_win = has('win32') || has('win64')

if s:is_win
  let s:c_compiler = "clang"
  let s:cpp_compiler = "clang++"
  let s:out_path = "%:p:h" . "/bin/" . "%:t:r" . ".exe"
else
  let s:c_compiler = "gcc"
  let s:cpp_compiler = "g++"
  let s:out_path = "%:p:h" . "/bin/" . "%:t:r"
endif

let s:c_flags = "-Wall -Wextra -std=c99"
let s:cpp_flags = "-Wall -Wextra -std=c++03"

" C debug build (default for .c)
command Make execute printf("let &makeprg = '%s -g -O0 %s %% -o %s'", s:c_compiler, s:c_flags, s:out_path)

" C final build
command MakeRelease execute printf("let &makeprg = '%s -O3 %s %% -o %s'", s:c_compiler, s:c_flags, s:out_path)

" C++ debug build (default for .cpp)
command Makecpp execute printf("let &makeprg = '%s -g -O0 %s %% -o %s'", s:cpp_compiler, s:cpp_flags, s:out_path)

" C++ final build
command MakecppRelease execute printf("let &makeprg = '%s -O3 %s %% -o %s'", s:cpp_compiler, s:cpp_flags, s:out_path)

" C fsyntax-only
command Makefsyntax execute printf("let &makeprg = '%s -fsyntax-only %s %%'", s:c_compiler, s:c_flags)

" C++ fsyntax-only
command Makecppfsyntax execute printf("let &makeprg = '%s -fsyntax-only %s %%'", s:cpp_compiler, s:cpp_flags)

" Copy makeprg to clipboard
command Cpmakeprg :let @+ = &makeprg | echo "==> \"" . @+ . "\" copied to the clipboard <=="

" Copy expanded makeprg to clipboard
command Cpmake :let @+ = expandcmd(&makeprg) | echo "==> \"" . @+ . "\" copied to the clipboard <=="

autocmd FileType c Makefsyntax
autocmd FileType cpp Makefsyntax

" `:redraw!` seems to be needed on my current linux terminal emulator
function s:BuildAndDebug()
  let l:prg_path = expand("%:p:h") . "/bin/" . expand("%:t:r")

  execute 'w'
  execute 'silent make!'

  if v:shell_error == 0
    " Lauch RAD Debugger on Windows
    if s:is_win
      call system(printf("start raddbg.exe \"%s.exe\"", l:prg_path))
      echo "==> Build Successful - RAD Debugger Launched <=="
    else
      let l:gdb_args = '-tui -ex "layout split" -ex "set print pretty" -ex "set logging file gdb_out.log" -ex "set logging enable on"'
      call system(printf("xfce4-terminal --execute gdb %s %s", l:gdb_args, l:prg_path))
      execute 'redraw!'
      echo "==> Build Successful - GDB Launched <=="
    endif
  else
    execute 'cw'
    echo "==> Build Failed - Opening Quickfix <=="
  endif
endfunction

function s:BuildAndCheck()
  execute 'w'
  execute 'silent make!'
  execute 'redraw!'

  " Check if the Quickfix Window have entries
  if len(getqflist()) > 0
    let @/ = "error"
    execute 'cw'
  endif
endfunction

command Raddbg call s:BuildAndDebug()
command Build call s:BuildAndCheck()
nnoremap <F10> :Raddbg<CR>
nnoremap <F5> :Build<CR>


"==============================
" Searching
"==============================
set incsearch         " Incremental search
set hlsearch          " Highlight search results
set ignorecase        " Ignore case when searching
set smartcase         " Respect case if the query contains uppercase letters
set showmatch         " Highlight matching parenthesis.

highlight Search cterm=bold ctermfg=Cyan gui=bold
highlight IncSearch cterm=bold,reverse ctermfg=White ctermbg=Red gui=bold
highlight CurSearch cterm=bold,reverse ctermfg=White ctermbg=Red gui=bold

set tags+=tags;
command Ctags silent execute '!ctags -R --c-kinds=+p .' | redraw!

"==============================
" Indentation
"==============================
set expandtab         " Use spaces instead of tabs
set tabstop=2         " Number of spaces a <Tab> counts for
set shiftwidth=2      " Number of spaces to use for auto-indentation
set softtabstop=2     " Number of spaces <Tab> inserts in edit mode
set autoindent        " Copy indent from current line when starting a new one
set cindent           " Use C-style indentation
set smartindent

" Make trailing whitespace chars visible
set list
set listchars=tab:▸\ ,trail:·


"==============================
" Misc & Netrw
"==============================
if s:is_win
  set shellslash
endif

let g:netrw_keepdir = 0
let mapleader = ","

" Taboo custom tab formatting
let g:taboo_tab_format = '%U[%N] %F%m  '
let g:taboo_renamed_tab_format = '[%N]%U %l%m  '

autocmd FileType netrw nnoremap <buffer> Y :let @+ = expand("%:p:h") . '/' . expand("<cfile>") " Copy file/dir path under the cursor on Netrw -> 'Y'
command Cpath :let @+ = expand("%:p")

nnoremap <leader>cpth :Cpath<CR> :echo "==> \"" . expand("%:p") . "\" copied to the clipboard <=="<CR>
nnoremap <leader>cop :copen<CR>
nnoremap <leader>ccl :cclose<CR>
nnoremap <leader>src :source $MYVIMRC <CR> :echo "==> sourced vimrc <=="<CR>
nnoremap <leader>erc :tab drop $MYVIMRC <bar> silent /makeprg<CR> zz

"==============================
" Cursor
"==============================
if s:is_win == 0
  let &t_SI = "\e[5 q" " Blinking bar for Insert mode
  let &t_SR = "\e[3 q" " Blinking underline for Replace mode
  let &t_EI = "\e[1 q" " Blinking block for Normal mode
endif
