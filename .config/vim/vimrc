set nocompatible

call plug#begin()
Plug 'bluz71/vim-nightfly-colors', { 'as': 'nightfly' }
call plug#end()

colorscheme nightfly "let s:black = '#010f20' on /vimfiles/plugged/nightfly/autoload/nightfly.vim
filetype plugin on

syntax enable
set number            " Show line numbers
set norelativenumber  " !Show relative line numbers
set scrolloff=8       " Keep 8 lines of context around the cursor
set backspace=indent,eol,start
set showcmd
set showcmdloc=last   " Show commands on the last line
set laststatus=2      " Always show statusline
set statusline=%f\ %m%r\ %=%l,%c\ %p%%


" --- Compiling & Debugging ---
let s:is_win = has('win32') || has('win64')

if s:is_win
  let s:c_compiler = "clang"
  let s:cpp_compiler = "clang++"
  let s:out_path = "%:p:h" . "/bin/" . "%:t:r" . ".exe"
else
  let s:c_compiler = "gcc"
  let s:cpp_compiler = "g++"
  let s:out_path = "%:p:h" . "/bin/" . "%:t:r"
endif

let s:c_flags = "-Wall -Wextra -std=c99"
let s:cpp_flags = "-Wall -Wextra -std=c++03"

" Default :make (C debug build)
command Make execute printf("let &makeprg = '%s -g -O0 %s %% -o %s'", s:c_compiler, s:c_flags, s:out_path)

" :MakeRelease (C final build)
command MakeRelease execute printf("let &makeprg = '%s -O3 %s %% -o %s'", s:c_compiler, s:c_flags, s:out_path)

" :Makecpp (C++ debug build)
command Makecpp execute printf("let &makeprg = '%s -g -O0 %s %% -o %s'", s:cpp_compiler, s:cpp_flags, s:out_path)

" :MakecppRelease (C++ final build)
command MakecppRelease execute printf("let &makeprg = '%s -O3 %s %% -o %s'", s:cpp_compiler, s:cpp_flags, s:out_path)

autocmd FileType c Make
autocmd FileType cpp Makecpp

function s:BuildAndDebug()
  execute 'w'
  execute 'silent make'

  if v:shell_error == 0
    " Lauch RAD Debugger on Windows
    if s:is_win
      call system(printf("start raddbg.exe \"%s/bin/%s.exe\"", expand("%:p:h"), expand("%:t:r")))
      echo "==> Build Successful - RAD Debugger Launched <=="
    else
      echo "==> Build Successful <=="
    endif
  else
    execute 'cw'
    echo "==> Build Failed - Opening Quickfix <=="
  endif
endfunction

command Raddbg call s:BuildAndDebug()
nnoremap <F10> :Raddbg<CR>
nnoremap <F5> :w <bar> silent make <bar> cw<CR>


" --- Searching ---
set incsearch         " Incremental search
set hlsearch          " Highlight search results
set ignorecase        " Ignore case when searching
set smartcase         " Respect case if the query contains uppercase letters
set showmatch         " Highlight matching parenthesis.

highlight Search cterm=bold ctermfg=Cyan
highlight IncSearch cterm=bold,reverse ctermfg=White ctermbg=Red gui=bold,reverse
highlight CurSearch cterm=bold,reverse ctermfg=White ctermbg=Red gui=bold,reverse


" --- Indentation ---
set expandtab         " Use spaces instead of tabs
set tabstop=2         " Number of spaces a <Tab> counts for
set shiftwidth=2      " Number of spaces to use for auto-indentation
set softtabstop=2     " Number of spaces <Tab> inserts in edit mode
set autoindent        " Copy indent from current line when starting a new one
set cindent           " Use C-style indentation
set smartindent

" Make trailing whitespace chars visible
set list
set listchars=tab:▸\ ,trail:·


" --- Misc & Netrw
if s:is_win
  set shellslash
endif

let g:netrw_keepdir = 0
let mapleader = ","

autocmd FileType * set formatoptions-=cro " Disable comment leader insertion
autocmd FileType netrw nnoremap <buffer> Y :let @+ = expand("%:p:h") . '/' . expand("<cfile>") " Copy file/dir path under the cursor on Netrw -> 'Y'
command Cpath :let @+ = expand("%:p")

nnoremap <leader>cpth :Cpath<CR> :echo "==> \"" . expand("%:p") . "\" copied to the clipboard <=="<CR>
nnoremap <leader>ccl :cclose<CR>
nnoremap <leader>src :source $MYVIMRC <CR> :echo "==> sourced vimrc <=="<CR>
nnoremap <leader>erc :tab drop $MYVIMRC <bar> silent /makeprg<CR> zz
